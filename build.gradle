plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.97'
}

final mcVersion = project.neo_version.split('\\.', 3).with {
    '1.' + it[0] + '.' + it[1]
}
version = mcVersion + "-" + mod_version
group = modGroup

repositories {
    maven {
        name = "BlameJared maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url(rootProject.file('../Titanium/repo'))
    }
}

base {
    archivesName = modBaseName
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

// Default run configurations.
// These can be tweaked, removed, or duplicated as needed.
runs {
    configureEach {
        // Recommended logging level for the console
        // You can set various levels here.
        // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
    }

    server {
        programArgument '--nogui'
    }

    data {
        // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
        // workingDirectory project.file('run-data')

        // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
        programArguments.addAll '--mod', 'functionalstorage', '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
    
	compileOnly "mezz.jei:jei-1.20.4-common-api:$jei_version"
    compileOnly "mezz.jei:jei-1.20.4-neoforge-api:$jei_version"
    runtimeOnly "mezz.jei:jei-1.20.4-neoforge:$jei_version"
	implementation "curse.maven:refined-storage-243076:5165548"

    implementation('com.hrznstudio:titanium')
    runtimeOnly('com.hrznstudio:Titanium:1.20.4-2.4.0')
}

tasks.configureEach {
    if (it.name.startsWith('neoForm')) it.enabled = false
}

jar {
    manifest {
        attributes(["Specification-Title"     : project.name,
                    "Specification-Vendor"    : "Buuz135",
                    "Specification-Version"   : "1.0",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${project.version}",
                    "Implementation-Vendor"   : "Buuz135",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    "ModSide"                 : "BOTH"
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
