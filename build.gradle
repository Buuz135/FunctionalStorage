plugins {
    id 'net.neoforged.moddev' version '0.1.120'
    id 'maven-publish'
}

final mcVersion = project.neo_version.split('\\.', 3).with {
    '1.' + it[0] + '.' + it[1]
}
version = mcVersion + "-" + mod_version
group = modGroup

sourceSets {
    main {
        resources.srcDir('src/generated/resources')
    }
}

repositories {
    maven {
        name = "BlameJared maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

base {
    archivesName = modBaseName
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = "${neo_version}"

    parchment {
        minecraftVersion = '1.21'
        mappingsVersion = '2024.06.23'
    }

    validateAccessTransformers = true

    runs {
        client {
            client()
        }
        server {
            server()
        }
        data {
            data()
            programArguments.addAll '--mod', 'functionalstorage', '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }

    mods {
        functionalstorage {
            sourceSet sourceSets.main
        }
        titanium {
            sourceSet sourceSets.main
        }
    }
}

dependencies {    
	compileOnly "mezz.jei:jei-1.21.1-common-api:$jei_version"
    compileOnly "mezz.jei:jei-1.21.1-neoforge-api:$jei_version"
    runtimeOnly "mezz.jei:jei-1.21.1-neoforge:$jei_version"
    implementation project.dependencies.create('com.hrznstudio:titanium:1.21-4.0.12')
    implementation "curse.maven:jade-324717:5444008"
    compileOnly "curse.maven:the-one-probe-245211:5475189"
    runtimeOnly "curse.maven:applied-energistics-2-223794:5729094"
}

jar {
    manifest {
        attributes(["Specification-Title"     : project.name,
                    "Specification-Vendor"    : "Buuz135, Rid",
                    "Specification-Version"   : "1.0",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${project.version}",
                    "Implementation-Vendor"   : "Buuz135, Rid",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    "ModSide"                 : "BOTH"
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    options.compilerArgs << '-Xmaxerrs' << '500'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      name 'repo'
      url(file('repo'))
    }
  }
}
